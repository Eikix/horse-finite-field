/* Interface */
// Naming those with FF prefix to not collide with add, mul, etc. reserved words
#define function FFAdd(uint64, uint64) view returns (uint64)
#define function FFSub(uint64, uint64) view returns (uint64)
#define function FFNeg(uint64) view returns (uint64)
#define function FFMul(uint64, uint64) view returns (uint64)
#define function FFPow(uint64, uint64) view returns (uint64)
#define function FFInverse(uint64) view returns (uint64)
#define function FFDiv(uint64, uint64) view returns (uint64)
#define function FFGetPrime() view returns (uint64)

/* Storage Slots */
// The prime for all the finite field operations
#define constant MODULUS = 0xD

/* Methods */
#define macro ADD() = takes (0) returns (0) {
    [MODULUS]    // [MODULUS]

    // @dev We suppose here that the function is defined as add(a, b) := a - b % MODULUS
    // `uint64 a` will be the first argument in the calldata, and `uint64 b` the second
    0x04 calldataload   // [MODULUS, a]
    0x24 calldataload // [b, a, MODULUS]

    // Utilise addmod opcode
    addmod              // [b + a % MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}


// If a >= b, then compute a - b using the SUB evm opcode
// If a < b, then compute MODULUS - (b - a)
// For example, for a = 6, b = 9, 6 - 9 = - 3 := MODULUS - 3 = MODULUS - (9 - 6)
#define macro SUB() = takes (0) returns (0) {

    // @dev We suppose here that the function is defined as sub(a, b) := a - b
    // `uint64 a` will be the first argument in the calldata, and `uint64 b` the second
    0x04 calldataload   // [a]
    0x24 calldataload // [b, a]

    dup1 // [b, b, a]
    dup3 // [a, b, b, a]

    lt // [a < b, b, a]
    iszero // [a >= b, b, a]
    standardSubstraction jumpi // [b, a]

    // Start the non standard flow: a < b:
    // we compute b - a (> 0)
    // Then return MODULUS - (b - a) == -(b - a) == (a - b) % MODULUS
    sub // [b - a]
    [MODULUS] // [MODULUS, b - a]
    sub // [MODULUS - (b - a)]

    // return
    0x00 mstore // []
    0x20 0x00 return


    standardSubstraction:
        swap1 // [a, b]
        sub // [a - b]

        // return
        0x00 mstore // []
        0x20 0x00 return
}

#define macro NEG() = takes (0) returns (0) {
    // @dev We suppose here that the function is defined as neg(a) := - a := MODULUS - a
    // `uint64 a` will be the first argument in the calldata
    0x04 calldataload   // [a]
    [MODULUS] // [MODULUS, a]

    // Utilise addmod opcode
    sub              // [MODULUS - a]

    // return
    0x00 mstore // []
    0x20 0x00 return
}

#define macro MUL() = takes (0) returns (0) {
    [MODULUS]    // [MODULUS]

    // @dev We suppose here that the function is defined as mul(a, b) := a * b % MODULUS
    // `uint64 a` will be the first argument in the calldata, and `uint64 b` the second
    0x04 calldataload   // [MODULUS, a]
    0x24 calldataload // [b, a, MODULUS]

    // Utilise addmod opcode
    mulmod              // [b * a % MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}

#define macro POW() = takes(0) returns(0) {
    [MODULUS] // [MODULUS]
    0x04 calldataload   // [a, MODULUS]
    0x24 calldataload // [exponent, a, MODULUS]
    swap1 // [a, exponent, MODULUS]
    exp // [a ** exponent, MODULUS]
    mod // [a ** exponent % MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}

// Inverse is defined as:
// a**(-1) := a^(modulus-2)
// Since: a^(modulus-1) == 1 <=> a^(modulus-2) * a == 1 <=> a**(-1) * a == 1
#define macro INVERSE() = takes(0) returns(0) {
    0x02 // [0x02]
    [MODULUS]  // [MODULUS, 0x02]
    sub // [MODULUS - 2]

    0x04 calldataload // [a, MODULUS - 2]
    exp // [a ** MODULUS - 2]
    [MODULUS] // [MODULUS, a ** MODULUS - 2]
    swap1 // [a ** MODULUS - 2, MODULUS]
    mod // [a ** MODULUS - 2 % MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}

#define macro DIV() = takes(0) returns(0) {

    // compute b**(-1)
    0x02 [MODULUS] sub // [MODULUS - 2]
    0x24 calldataload // [b, MODULUS - 2]
    exp // [b ** MODULUS - 2]
    [MODULUS] swap1 // [b ** MODULUS - 2, MODULUS]
    mod // [b ** MODULUS - 2 % MODULUS]

    // multiply a * b**(-1)
    0x04 calldataload // [a, b ** MODULUS - 2 % MODULUS]
    [MODULUS] // [MODULUS, a, b ** MODULUS - 2 % MODULUS]
    swap2 // [b ** MODULUS - 2 % MODULUS, a, MODULUS]
    swap1 // [a, b**-1, MODULUS]
    mulmod // [a * b**-1 % MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}

#define macro GET_PRIME() = takes(0) returns(0) {
    [MODULUS] // [MODULUS]

    // return
    0x00 mstore // []
    0x20 0x00 return
}



#define macro MAIN() = takes (0) returns (0) {
    // Identify which function is being called.
    0x00 calldataload 0xE0 shr // [sig]
    dup1 __FUNC_SIG(FFAdd) eq ff_add jumpi
    dup1 __FUNC_SIG(FFSub) eq ff_sub jumpi
    dup1 __FUNC_SIG(FFNeg) eq ff_neg jumpi
    dup1 __FUNC_SIG(FFMul) eq ff_mul jumpi
    dup1 __FUNC_SIG(FFPow) eq ff_pow jumpi
    dup1 __FUNC_SIG(FFInverse) eq ff_inverse jumpi
    dup1 __FUNC_SIG(FFDiv) eq ff_div jumpi
    dup1 __FUNC_SIG(FFGetPrime) eq ff_get_prime jumpi

    0x00 0x00 revert

    ff_add:
        ADD()
    ff_sub:
        SUB()
    ff_neg:
        NEG()
    ff_mul:
        MUL()
    ff_pow:
        POW()
    ff_inverse:
        INVERSE()
    ff_div:
        DIV()
    ff_get_prime:
        GET_PRIME()
}